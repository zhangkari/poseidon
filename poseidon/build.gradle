apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'

def build = depends.build
def support = depends.support
def test = depends.test

def USER = "zhangkari"
def GROUP = "com.class100.poseidon"
def ARTIFACT = "poseidon"
def VCode = depends.artifact.VCode
def VName = depends.artifact.VName
def DESCRIPTION = "lib Poseidon for android infra"
def URL = "https://github.com/zhangkari/poseidon"

android {
    compileSdkVersion build.compileSdkVersion

    defaultConfig {
        minSdkVersion build.minSdkVersion
        targetSdkVersion build.compileSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publish {
        userOrg = USER
        groupId = GROUP
        artifactId = ARTIFACT
        publishVersion = VName
        desc = DESCRIPTION
        website = URL
    }

}

def isRelease() {
    return !depends.artifact.VName.contains("-")
}

def getRepoUrl() {
    def url = NEXUS_HOST_URL + "/repository"
    def releasesUrl = url + "/maven-releases/"
    def snapshotUrl = url + "/maven-snapshots/"
    isRelease() ? releasesUrl : snapshotUrl
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = GROUP
            pom.artifactId = ARTIFACT
            pom.version = VName
            repository(url: getRepoUrl()) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
        }
    }
    println("uploadArchives:")
    println("###################################################")
    println("url => " + getRepoUrl())
    println("artifact => " + GROUP + ":" + ARTIFACT + ":" + VName)
    println("###################################################")
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.aar'])

    implementation support.appcompat
    implementation 'com.tencent.tbs.tbssdk:sdk:43939'
    testImplementation test.junit
    androidTestImplementation test.andJunit
    androidTestImplementation test.espresso
}